plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id "io.gitlab.arturbosch.detekt"
    // detekt STEP1 applying plugin (detekt = kotlin code's static analyser)
    id "checkstyle"
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
    // checkstyle STEP1 applying plugin (checkstyle = java code's static analyser)

}

// detekt STEP2 plugin configuration
detekt {
    parallel = true
    allRules = true
    ignoreFailures = true
    debug = true
}

// detekt STEP3 gradle task configuration
tasks.named("detekt").configure {
    reports {
        html.required.set(true)
        sarif.required.set(false)
        xml.required.set(false)
        txt.required.set(false)
    }
}

// checkstyle STEP2 plugin configuration
checkstyle {
    ignoreFailures false
    showViolations true
    toolVersion = "7.8.1"
}

// checkstyle STEP3 gradle task configuration
task Checkstyle(type: Checkstyle) {
    source 'src/main/java'
    ignoreFailures true
    showViolations true
    include '**/*.java'
    classpath = files()
}

android {
    namespace 'com.example.cicdgithubactions'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.cicdgithubactions"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ext.config = getProps("$rootDir/config/release.properties")
            ext.config.each { p ->

                if (p.key == "APPNAME") {
                    resValue "string", "app_name", p.value
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else
                    buildConfigField 'String', p.key, p.value
            }
        }

        staging {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ext.config = getProps("$rootDir/config/staging.properties")
            ext.config.each { p ->
                if (p.key == "APPNAME") {
                    resValue "string", "app_name", p.value.replace('"', '')
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else {
                    buildConfigField 'String', p.key, p.value
                }
            }
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ext.config = getProps("$rootDir/config/development.properties")
            ext.config.each { p ->
                if (p.key == "APPNAME") {
                    resValue "string", "app_name", p.value
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else
                    buildConfigField 'String', p.key, p.value
            }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

static def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File(path)))
    return props
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}